cmake_minimum_required(VERSION 3.10)
project(Cactus LANGUAGES CXX)

set(CMAKE_CXX_STANDARD 20)
set(CMAKE_CXX_STANDARD_REQUIRED True)

set(CMAKE_OSX_DEPLOYMENT_TARGET "13.0" CACHE STRING "Minimum macOS version")
set(CMAKE_XCODE_ATTRIBUTE_IPHONEOS_DEPLOYMENT_TARGET "16.0" CACHE STRING "Minimum iOS version")

set(SOURCE_DIR ${CMAKE_SOURCE_DIR}/../cactus)
set(CACTUS_CURL_ROOT "${CMAKE_SOURCE_DIR}/../libs/curl" CACHE PATH "Path to vendored libcurl artifacts")

if(NOT CMAKE_BUILD_TYPE)
    set(CMAKE_BUILD_TYPE Release)
    message(STATUS "CMAKE_BUILD_TYPE was not set, defaulting to Release.")
endif()

file(GLOB ENGINE_SOURCES "${SOURCE_DIR}/engine/*.cpp")
file(GLOB GRAPH_SOURCES "${SOURCE_DIR}/graph/*.cpp")
file(GLOB KERNEL_SOURCES "${SOURCE_DIR}/kernel/*.cpp")
file(GLOB FFI_SOURCES "${SOURCE_DIR}/ffi/*.cpp")
file(GLOB MODEL_SOURCES "${SOURCE_DIR}/models/*.cpp")
file(GLOB TELEMETRY_SOURCES "${SOURCE_DIR}/telemetry/*.cpp")

enable_language(OBJCXX)

set(NPU_SOURCES "${SOURCE_DIR}/npu/npu_ane.mm")

if(CMAKE_SYSTEM_NAME STREQUAL "iOS")
    string(TOLOWER "${CMAKE_OSX_SYSROOT}" SYSROOT_LOWER)
    if(SYSROOT_LOWER MATCHES "simulator")
        message(STATUS "Building for iOS Simulator with ANE support")
    else()
        message(STATUS "Building for iOS Device with ANE support")
    endif()
else()
    message(STATUS "Building for macOS with ANE support")
endif()

set(HEADER_FILES
    ${SOURCE_DIR}/cactus.h
    ${SOURCE_DIR}/engine/engine.h
    ${SOURCE_DIR}/graph/graph.h
    ${SOURCE_DIR}/kernel/kernel.h
    ${SOURCE_DIR}/kernel/kernel_utils.h
    ${SOURCE_DIR}/ffi/cactus_ffi.h
    ${SOURCE_DIR}/ffi/cactus_utils.h
    ${SOURCE_DIR}/models/model.h
    ${SOURCE_DIR}/npu/npu.h
)

set(COMMON_SOURCES
    ${KERNEL_SOURCES}
    ${GRAPH_SOURCES}
    ${ENGINE_SOURCES}
    ${FFI_SOURCES}
    ${MODEL_SOURCES}
    ${TELEMETRY_SOURCES}
    ${NPU_SOURCES}
)

if(NOT COMMON_SOURCES)
    message(FATAL_ERROR "No source files found! Check SOURCE_DIR: ${SOURCE_DIR}")
endif()

if(BUILD_SHARED_LIBS)
    add_library(cactus SHARED ${COMMON_SOURCES})
    
    if(CMAKE_SYSTEM_NAME STREQUAL "iOS" OR CMAKE_SYSTEM_NAME STREQUAL "Darwin")
        set_target_properties(cactus PROPERTIES
            FRAMEWORK TRUE
            FRAMEWORK_VERSION A
            MACOSX_FRAMEWORK_IDENTIFIER com.cactuscompute.cactus
            VERSION 1.0.0
            SOVERSION 1.0.0
            PUBLIC_HEADER "${SOURCE_DIR}/ffi/cactus_ffi.h;${SOURCE_DIR}/engine/engine.h;${SOURCE_DIR}/graph/graph.h;${SOURCE_DIR}/kernel/kernel.h;${SOURCE_DIR}/cactus.h"
            MACOSX_FRAMEWORK_INFO_PLIST "${CMAKE_SOURCE_DIR}/Info.plist"
        )
    endif()
else()
    add_library(cactus STATIC ${COMMON_SOURCES})
endif()

target_include_directories(cactus PUBLIC
    ${SOURCE_DIR}
    ${SOURCE_DIR}/engine
    ${SOURCE_DIR}/graph
    ${SOURCE_DIR}/kernel
    ${SOURCE_DIR}/ffi
    ${SOURCE_DIR}/telemetry
    ${SOURCE_DIR}/npu
)

find_library(COREML_FRAMEWORK CoreML REQUIRED)
find_library(FOUNDATION_FRAMEWORK Foundation REQUIRED)
find_library(ACCELERATE_FRAMEWORK Accelerate REQUIRED)
find_library(SECURITY_FRAMEWORK Security REQUIRED)
find_library(SYSTEMCONFIGURATION_FRAMEWORK SystemConfiguration REQUIRED)
find_library(CFNETWORK_FRAMEWORK CFNetwork REQUIRED)

if(CMAKE_SYSTEM_NAME STREQUAL "Darwin")
    set(CACTUS_CURL_INCLUDE_DIR "${CACTUS_CURL_ROOT}/include")
    set(CACTUS_CURL_LIBRARY_MACOS "${CACTUS_CURL_ROOT}/macos/libcurl.a")
    if(NOT EXISTS "${CACTUS_CURL_INCLUDE_DIR}/curl/curl.h" OR NOT EXISTS "${CACTUS_CURL_LIBRARY_MACOS}")
        message(FATAL_ERROR "Vendored macOS libcurl not found. Expected: ${CACTUS_CURL_INCLUDE_DIR}/curl/curl.h and ${CACTUS_CURL_LIBRARY_MACOS}")
    endif()
    message(STATUS "Using vendored libcurl for macOS: ${CACTUS_CURL_LIBRARY_MACOS}")
    target_link_libraries(cactus PUBLIC
        ${COREML_FRAMEWORK}
        ${FOUNDATION_FRAMEWORK}
        ${ACCELERATE_FRAMEWORK}
        ${SECURITY_FRAMEWORK}
        ${SYSTEMCONFIGURATION_FRAMEWORK}
        ${CFNETWORK_FRAMEWORK}
        "${CACTUS_CURL_LIBRARY_MACOS}"
    )
    target_include_directories(cactus PUBLIC "${CACTUS_CURL_INCLUDE_DIR}")
    target_compile_definitions(cactus PRIVATE CACTUS_USE_CURL=1)
elseif(CMAKE_SYSTEM_NAME STREQUAL "iOS")
    target_link_libraries(cactus PUBLIC
        ${COREML_FRAMEWORK}
        ${FOUNDATION_FRAMEWORK}
        ${ACCELERATE_FRAMEWORK}
        ${SECURITY_FRAMEWORK}
        ${SYSTEMCONFIGURATION_FRAMEWORK}
        ${CFNETWORK_FRAMEWORK}
    )
    set(CACTUS_CURL_INCLUDE_DIR "${CACTUS_CURL_ROOT}/include")
    string(TOLOWER "${CMAKE_OSX_SYSROOT}" CACTUS_SYSROOT_LOWER)
    if(CACTUS_SYSROOT_LOWER MATCHES "simulator")
        set(CACTUS_CURL_LIBRARY_IOS "${CACTUS_CURL_ROOT}/ios/simulator/libcurl.a")
    else()
        set(CACTUS_CURL_LIBRARY_IOS "${CACTUS_CURL_ROOT}/ios/device/libcurl.a")
    endif()
    if(NOT EXISTS "${CACTUS_CURL_INCLUDE_DIR}/curl/curl.h" OR NOT EXISTS "${CACTUS_CURL_LIBRARY_IOS}")
        message(FATAL_ERROR "Vendored iOS libcurl not found for sysroot ${CMAKE_OSX_SYSROOT}. Expected: ${CACTUS_CURL_INCLUDE_DIR}/curl/curl.h and ${CACTUS_CURL_LIBRARY_IOS}")
    endif()
    message(STATUS "Using vendored libcurl for iOS: ${CACTUS_CURL_LIBRARY_IOS}")
    target_link_libraries(cactus PUBLIC "${CACTUS_CURL_LIBRARY_IOS}")
    target_include_directories(cactus PUBLIC "${CACTUS_CURL_INCLUDE_DIR}")
    target_compile_definitions(cactus PRIVATE CACTUS_USE_CURL=1)
else()
    target_link_libraries(cactus PUBLIC ${COREML_FRAMEWORK} ${FOUNDATION_FRAMEWORK} ${ACCELERATE_FRAMEWORK})
endif()

target_compile_definitions(cactus PRIVATE
    PLATFORM_CPU_ONLY=1
)

if(CMAKE_SYSTEM_NAME STREQUAL "iOS")
    target_compile_definitions(cactus PRIVATE TARGET_OS_IPHONE=1)
endif()

target_compile_options(cactus PRIVATE 
    -pthread
    -Wall
    -Wextra
    -pedantic
    -Wno-c++20-designator 
    -Wno-missing-field-initializers 
    -fomit-frame-pointer
    -funroll-loops
    -ftree-vectorize
)

if (${CMAKE_BUILD_TYPE} STREQUAL "Debug")
    target_compile_options(cactus PRIVATE -DCACTUS_IOS_ENABLE_LOGGING)
endif ()

target_compile_options(cactus PRIVATE -O3 -DNDEBUG)
target_compile_options(cactus PRIVATE -fvisibility=hidden -fvisibility-inlines-hidden)
target_compile_options(cactus PRIVATE -ffunction-sections -fdata-sections)

target_compile_options(cactus PRIVATE
    -march=armv8.2-a+fp16+simd+dotprod+i8mm
)

target_compile_definitions(cactus PRIVATE
    __ARM_NEON=1
    __ARM_FEATURE_FP16_VECTOR_ARITHMETIC=1
    __ARM_FEATURE_DOTPROD=1
    __ARM_FEATURE_MATMUL_INT8=1
)

target_link_options(cactus PRIVATE -Wl,-dead_strip)
target_link_options(cactus PRIVATE -flto)

set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/lib)
set(CMAKE_LIBRARY_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/lib)
set(CMAKE_RUNTIME_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/bin)
